import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.minecraftforge.snowblower.gradle.DependencyHashingTask
import net.minecraftforge.snowblower.gradle.ShadowJarFixupTask

plugins {
    id 'java'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.minecraftforge.gradleutils' version '2.+'
}

group 'net.minecraftforge'
version = gradleutils.getTagOffsetVersion()
println "Version: $version"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()

    maven gradleutils.forgeMaven
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.0.0'

    // When updating shade dependencies, you must also run the generateDependencyHashes task
    shade 'org.eclipse.jgit:org.eclipse.jgit:6.4.0.202211300538-r'
    shade 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    shade 'com.google.code.gson:gson:2.10.1'
    shade 'net.minecraftforge:srgutils:0.5.1'
    shade 'net.minecraftforge:mergetool:1.1.5'
    shade 'net.minecraftforge:ForgeAutoRenamingTool:0.1.24'
    shade 'net.minecraftforge:forgeflower:2.0.627.0'
    shade 'net.minecraftforge:installertools:1.3.2'
}

tasks.register('generateDependencyHashes', DependencyHashingTask) {
    configuration = configurations.shade
    output = file('src/main/generated_resources/dependency_hashes.txt')
}

tasks.register('shadowJarFixup', ShadowJarFixupTask) {
    outputs.upToDateWhen { false }
    input = shadowJar.archiveFile
    output = shadowJar.archiveFile
}

afterEvaluate {
    tasks.named('generateMetadataFileForMavenJavaPublication').configure {
        dependsOn 'shadowJarFixup'
    }
}

sourceSets {
    main {
        resources {
            srcDirs += file('src/main/generated_resources')
        }
    }
}

tasks.named('processResources', ProcessResources).configure {
    dependsOn 'generateDependencyHashes'
    from 'gradlew'
    from 'gradlew.bat'
}

tasks.named('eclipseClasspath', GenerateEclipseClasspath).configure {
    dependsOn 'generateDependencyHashes'
}

tasks.named('idea').configure {
    dependsOn 'generateDependencyHashes'
}

tasks.named('jar', Jar).configure {
    from('gradle') {
        into 'gradle'
    }
    manifest {
        attributes([
            'Main-Class': 'net.minecraftforge.snowblower.Main',
            'Implementation-Version': "${project.version} (${gradleutils.gitInfo.commit})"
        ])
    }
}

tasks.named('shadowJar', ShadowJar).configure {
    archiveClassifier = 'all'
    configurations = [project.configurations.shade]

    from('gradle') {
        into 'gradle'
        rename '(.+).jar', '$1.zip'
    }

    minimize()

    finalizedBy 'shadowJarFixup'
}

artifacts {
    archives shadowJar
}

license {
    header = file('HEADER')
    ext.fullname = 'Forge Development LLC'
    exclude '**/*.properties'
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}

changelog {
    fromCommit '4f8480ec50cc76a562230245170172bc84777d3a'
}